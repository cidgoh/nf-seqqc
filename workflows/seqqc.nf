/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    VALIDATE INPUTS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

// Check if database filepaths exists
public static Boolean fileExists(filename) {
    return new File(filename).exists()
}
def summary_params = NfcoreSchema.paramsSummaryMap(workflow, params)

// Validate input parameters
WorkflowSeqqc.initialise(params, log)

// TODO nf-core: Add all file path parameters for the pipeline to the list below
// Check input path parameters to see if they exist
def checkPathParamList = [  params.multiqc_config, params.fasta ]
for (param in checkPathParamList) { if (param) { file(param, checkIfExists: true) } }

// Check mandatory parameters
if (params.input) { ch_input = file(params.input) } else { exit 1, 'Input samplesheet not specified!' }

// Check if input database paths are valid
if (!params.skip_kraken2 && !fileExists(params.kraken2_db)) {
    log.error "Path to Kraken2 database is not valid"
    exit 1
}
if ((params.classifier == 'centrifuge') && !fileExists(params.centrifuge_db)) {
    log.error "Path to Centrifuge database is not valid"
    exit 1
}
if (!params.skip_checkm && !fileExists(params.checkm_db)) {
    log.error "Path to CheckM database is not valid"
    exit 1
}
if (!params.skip_confindr && !fileExists(params.confindr_db)) {
    log.error "Path to Confindr database is not valid"
    exit 1
}
if (!params.skip_busco && !fileExists(params.busco_lineages_path)) {
    log.error "Path to BUSCO lineages database is not valid"
    exit 1
}
/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    CONFIG FILES
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

ch_multiqc_config          = Channel.fromPath("$projectDir/assets/multiqc_config.yml", checkIfExists: true)
ch_multiqc_custom_config   = params.multiqc_config ? Channel.fromPath( params.multiqc_config, checkIfExists: true ) : Channel.empty()
ch_multiqc_logo            = params.multiqc_logo   ? Channel.fromPath( params.multiqc_logo, checkIfExists: true ) : Channel.empty()
ch_multiqc_custom_methods_description = params.multiqc_methods_description ? file(params.multiqc_methods_description, checkIfExists: true) : file("$projectDir/assets/methods_description_template.yml", checkIfExists: true)

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT LOCAL MODULES/SUBWORKFLOWS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

//
// SUBWORKFLOW: Consisting of a mix of local and nf-core/modules
//
include { INPUT_CHECK           } from '../subworkflows/local/input_check'
include { WGS_ASSEMBLY          } from '../subworkflows/local/wgs_assembly'
include { ASSEMBLY_QC           } from '../subworkflows/local/assembly_qc'
include { RSMLST                } from '../subworkflows/local/rmlst'
include { TAXONOMY_QC           } from '../subworkflows/local/taxonomy_qc'
include { RAW_READS_QC          } from '../subworkflows/local/raw_reads_qc'
include { CAT_NANOPORE_FASTQ    } from '../modules/local/cat_nanopore_fastq/main'
include { NANOPORE_RAW_READS_QC } from '../subworkflows/local/nanopore_raw_reads_qc'

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT NF-CORE MODULES/SUBWORKFLOWS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

//
// MODULE: Installed directly from nf-core/modules
//
include { FASTQC                      } from '../modules/nf-core/fastqc/main'
include { MULTIQC                     } from '../modules/nf-core/multiqc/main'
include { CUSTOM_DUMPSOFTWAREVERSIONS } from '../modules/nf-core/custom/dumpsoftwareversions/main'


/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    RUN MAIN WORKFLOW
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

// Info required for completion email and summary
def multiqc_report = []

workflow SEQQC {

    main:
    ch_versions = Channel.empty()

    //
    // SUBWORKFLOW: Read in samplesheet, validate and stage input files
    //
    INPUT_CHECK (
        ch_input
    )
    ch_versions = ch_versions.mix(INPUT_CHECK.out.versions)

    if (params.mode=='nanopore') {
        // Concatenate fastq files
        CAT_NANOPORE_FASTQ(INPUT_CHECK.out.reads)

        ch_merged_reads = CAT_NANOPORE_FASTQ.out.reads
    }

    //
    // SUBWORKFLOW: QC sub-workflow
    //
    if (!params.skip_QC){
        // SUBWORKFLOW: Run raw reads QC on nanopore reads
        // Concatenate reads of each barcode into one
        if (params.mode=="nanopore") {
            NANOPORE_RAW_READS_QC(
                ch_merged_reads,
                params.nanopore_summary_file
            )
            ch_reads_qc = NANOPORE_RAW_READS_QC.out.merged_reads
        }
        else {
            if(!params.skip_raw_qc){
                ch_raw_reads_qc = INPUT_CHECK.out.reads
                RAW_READS_QC(ch_raw_reads_qc)
                ch_reads_qc = RAW_READS_QC.out.short_reads
                ch_assembly_reads = RAW_READS_QC.out.short_reads
            }
            else{
                ch_reads_qc = INPUT_CHECK.out.reads
            }
        }
        if(!params.skip_taxonomy_qc){
            TAXONOMY_QC (
                ch_reads_qc,
                params.host_genome
                )
                ch_assembly_reads = TAXONOMY_QC.out.ch_tax_qc_reads
            }
        else{
                ch_assembly_reads = ch_reads_qc
            }
    }
    else{
        if (params.mode=='nanopore'){
            ch_assembly_reads = ch_merged_reads
        }
        else {
            ch_assembly_reads = INPUT_CHECK.out.reads
        }
    }

    if (!params.skip_assembly){
        // SUBWORKFLOW: Run WGS ASSEMBLY on reads
        WGS_ASSEMBLY(
            ch_assembly_reads
        )

        if (!params.skip_assembly_qc){
            // SUBWORKFLOW: Do ribosomal MLST on assembled contigs, using BIGSdb Restful API
            if (!params.skip_rmlst){
                RSMLST(
                    WGS_ASSEMBLY.out.ch_contigs
                )
            }
            // SUBWORKFLOW: RUN ASSEMBLY QC on assemblies
            ASSEMBLY_QC(
                WGS_ASSEMBLY.out.ch_contigs
            )
        }
    }
    //ch_versions = ch_versions.mix(TAXONOMY_QC.out.versions.first())

    CUSTOM_DUMPSOFTWAREVERSIONS (
        ch_versions.unique().collectFile(name: 'collated_versions.yml')
    )


    //
    // MODULE: MultiQC
    //
    workflow_summary    = WorkflowSeqqc.paramsSummaryMultiqc(workflow, summary_params)
    ch_workflow_summary = Channel.value(workflow_summary)

    methods_description    = WorkflowSeqqc.methodsDescriptionText(workflow, ch_multiqc_custom_methods_description)
    ch_methods_description = Channel.value(methods_description)

    ch_multiqc_files = Channel.empty()
    ch_multiqc_files = ch_multiqc_files.mix(ch_workflow_summary.collectFile(name: 'workflow_summary_mqc.yaml'))
    ch_multiqc_files = ch_multiqc_files.mix(ch_methods_description.collectFile(name: 'methods_description_mqc.yaml'))
    ch_multiqc_files = ch_multiqc_files.mix(CUSTOM_DUMPSOFTWAREVERSIONS.out.mqc_yml.collect())
    //ch_multiqc_files = ch_multiqc_files.mix(FASTQC.out.zip.collect{it[1]}.ifEmpty([]))

    MULTIQC (
        ch_multiqc_files.collect(),
        ch_multiqc_config.toList(),
        ch_multiqc_custom_config.toList(),
        ch_multiqc_logo.toList()
    )
    multiqc_report = MULTIQC.out.report.toList()

}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    COMPLETION EMAIL AND SUMMARY
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

workflow.onComplete {
    if (params.email || params.email_on_fail) {
        NfcoreTemplate.email(workflow, params, summary_params, projectDir, log, multiqc_report)
    }
    NfcoreTemplate.summary(workflow, params, log)
    if (params.hook_url) {
        NfcoreTemplate.IM_notification(workflow, params, summary_params, projectDir, log)
    }
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    THE END
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
